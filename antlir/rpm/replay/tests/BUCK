load("//antlir/bzl:image.bzl", "image")
load("//antlir/bzl:layer_resource.bzl", "layer_resource")
load("//antlir/bzl:oss_shim.bzl", "python_library", "python_unittest")
load("//antlir/rpm/replay:queries.bzl", "ANTLIR_BUILD_PKG_LABEL", "test_env_map")

python_unittest(
    name = "test-fake-pty",
    srcs = ["test_fake_pty.py"],
    needed_coverage = [(
        100,
        "//antlir/rpm/replay:fake_pty",
    )],
    deps = [
        "//antlir:fs_utils",
        "//antlir/rpm/replay:fake_pty",
    ],
)

python_library(
    name = "test_utils",
    srcs = ["test_utils.py"],
    visibility = ["//tupperware/cm/antlir/tests/..."],
    deps = [
        "//antlir/rpm/replay:extract_nested_features",
        "//antlir/rpm/replay:replay_util",
    ],
)

##
## Test layers for `extract_nested_features`
##

image.layer(
    name = "potato",
    features = [image.ensure_dirs_exist("/i_am_potato")],
    flavor = "antlir_test",
)

image.layer(
    name = "base-original",
    features = [
        image.ensure_dirs_exist("/from/test_base/"),
        # Without an rpms_install, /var/lib/rpm/ won't exist.
        # This prevents subvol-rpm-compare from querying or installing rpms.
        image.rpms_install(["rpm-test-milk"]),
    ],
    flavor = "antlir_test",
)

image.package(
    name = "base.sendstream",
    format = "sendstream",
    layer = ":base-original",
    labels = [ANTLIR_BUILD_PKG_LABEL],
)

image.layer_from_package(
    name = "base",
    format = "sendstream",
    source = ":base.sendstream",
    flavor = "antlir_test",
    visibility = ["//tupperware/cm/antlir/tests/..."],
)

image.layer(
    name = "base-plus-one",
    parent_layer = ":base",
    flavor = "antlir_test",
    features = [
        # All of these are "non-custom" as far as `extract_nested_features`
        # is concerned.
        image.ensure_dirs_exist("/new/dir/"),
        image.rpms_install(["rpm-test-milk"]),
        image.layer_mount(":potato", "/vegetable"),
    ],
)

image.layer(
    name = "custom",
    parent_layer = ":base-plus-one",
    flavor = "antlir_test",
    # Removal triggers the "custom image" code path.
    features = [image.remove("/new/dir/")],
)

image.layer(
    name = "custom-remove-rpm",
    parent_layer = ":base-plus-one",
    flavor = "antlir_test",
    features = [image.rpms_remove_if_exists(["rpm-test-milk"])],
)

# TODO(lesha): Fold this into `non-custom` soon
image.layer(
    name = "custom-local-rpm",
    parent_layer = ":base-plus-one",
    flavor = "antlir_test",
    features = [image.rpms_install([
        "//antlir/compiler/test_images:rpm-test-cheese-2-1.rpm",
    ])],
)

image.layer(
    name = "non-custom",
    parent_layer = ":base-plus-one",
    flavor = "antlir_test",
    # Another non-custom feature.  This is a sibling of `custom` to show
    # that the depth does not trigger custom vs non-custom behavior.
    features = [image.ensure_dirs_exist("/another/dir/")],
)

python_unittest(
    name = "test-extract-nested-features",
    srcs = ["test_extract_nested_features.py"],
    env = test_env_map([
        ("custom", "//antlir/rpm/replay/tests:custom"),
        ("custom-remove-rpm", "//antlir/rpm/replay/tests:custom-remove-rpm"),
        ("custom-local-rpm", "//antlir/rpm/replay/tests:custom-local-rpm"),
        ("non-custom", "//antlir/rpm/replay/tests:non-custom"),
    ]),
    needed_coverage = [
        (100, "//antlir/rpm/replay:extract_nested_features"),
    ],
    deps = [
        ":test_utils",
        "//antlir/rpm/replay:extract_nested_features",
    ],
)

##
## Test layers used in rpm replay tests
##

image.layer(
    name = "leaf",
    parent_layer = ":base",
    features = [
        image.rpms_install([
            "rpm-test-has-epoch",
            "rpm-test-second",
            "rpm-test-fifth",
        ]),
        image.ensure_subdirs_exist("/", "hello/there"),
    ],
    flavor = "antlir_test",
    flavor_config_override = image.opts(
        rpm_repo_snapshot = "//antlir/rpm:rpm-replay-repo-snapshot-for-tests",
    ),
    visibility = ["//tupperware/cm/antlir/tests/..."],
)

# Only test-subvol-rpm-compare supports rpm removal
image.layer(
    name = "test-subvol-rpm-compare-leaf",
    parent_layer = ":leaf",
    features = [image.rpms_remove_if_exists(["rpm-test-milk"])],
    flavor = "antlir_test",
)

python_unittest(
    name = "test-subvol-rpm-compare",
    srcs = ["test_subvol_rpm_compare.py"],
    needed_coverage = [
        (100, "//antlir/rpm/replay:subvol_rpm_compare"),
    ],
    resources = {
        layer_resource(":base"): "root_subvol",
        layer_resource("//antlir/compiler/test_images:build_appliance_testing"): "ba_subvol",
        layer_resource(":test-subvol-rpm-compare-leaf"): "leaf_subvol",
    },
    deps = [
        "//antlir:subvol_utils",
        "//antlir:testlib_layer_resource",
        "//antlir/rpm:find_snapshot",
        "//antlir/rpm/replay:subvol_rpm_compare",
    ],
)

python_unittest(
    name = "test-rpm-replay",
    srcs = ["test_rpm_replay.py"],
    env = test_env_map([("leaf", ":leaf")]),
    needed_coverage = [
        (100, "//antlir/rpm/replay:rpm_replay"),
    ],
    resources = {
        layer_resource(":base"): "root_subvol",
        layer_resource("//antlir/compiler/test_images:build_appliance_testing"): "ba_subvol",
        layer_resource(":leaf"): "leaf_subvol",
    },
    deps = [
        ":test_utils",
        "//antlir:config",
        "//antlir:subvol_utils",
        "//antlir:testlib_layer_resource",
        "//antlir/compiler:items_for_features",
        "//antlir/rpm/replay:extract_nested_features",
        "//antlir/rpm/replay:rpm_replay",
        "//antlir/rpm/replay:subvol_diff",
        "//antlir/rpm/replay:subvol_rpm_compare",
    ],
)
