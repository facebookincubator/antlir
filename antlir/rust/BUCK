# @oss-disable: 
load("//antlir/bzl:build_defs.bzl", "export_file", "python_library", "rust_binary", "rust_python_extension", "write_file")
load(":modules.bzl", "extension_modules", "extension_rust_targets")

oncall("antlir")

# Main entrypoint to the rust python extension modules
# This exists because Python import resolution is annoying and would otherwise
# make it impossible to have any python_{library,binary,unittest} targets under
# antlir/rust
python_library(
    name = "rust",
    srcs = ["__init__.py"],
    deps = [":native_antlir_impl"],
)

rust_python_extension(
    name = "native_antlir_impl",
    srcs = ["src/lib.rs"],
    compatible_with = [
        "ovr_config//os:freebsd",
        "ovr_config//os:linux",
        "ovr_config//os:macos",
    ],
    mapped_srcs = {
        ":register_modules.rs": "src/register_modules.rs",
    },
    visibility = ["//antlir/..."],
    deps = ["pyo3"] + extension_rust_targets,
)

export_file(name = "trigger_rust_module_init.py")

write_file(
    name = "register_modules.rs",
    out = "register_modules.rs",
    content = [
        "use pyo3::prelude::*;",
        "pub(crate) fn register_modules(py: Python<'_>, m: &Bound<PyModule>) -> PyResult<()> {",
    ] + [
        'submodule!({}, "{}", py, m)?;'.format(crate, module)
        for crate, module in extension_modules.items()
    ] + [
        "Ok(())",
        "}",
    ],
)

rust_binary(
    name = "gen-modules-bzl",
    srcs = [
        "gen_modules_bzl.rs",
        "modules.bzl",
    ],
    crate_root = "gen_modules_bzl.rs",
    test_deps = [
        "pretty_assertions",
    ],
    # Test relies on the repo which is not present on aarch64 RE
    # @oss-disable: 
    deps = [
        "anyhow",
        "serde",
        "serde_json",
        "serde_starlark",
        "//antlir/buck2/buck_label:buck_label",
        "//antlir/signedsource:signedsource",
    ],
)
