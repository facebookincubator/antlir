load("//antlir/bzl:image.bzl", "image")
load("//antlir/bzl:image_unittest_helpers.bzl", helpers = "image_unittest_helpers")
load("//antlir/bzl:oss_shim.bzl", "buck_genrule", "export_file", "python_unittest")
load("//antlir/bzl:vm.bzl", "vm")
load("//antlir/vm:kernel.bzl", "make_kernel_artifacts")

# Don't run this test directly, it should only be used within the
# `kernel_panic_test`.
vm.python_unittest(
    name = "create-kernel-panic",
    srcs = ["create_kernel_panic.py"],
    tags = helpers.tags_to_hide_test(),
    visibility = [],
)

python_unittest(
    name = "test-kernel-panic",
    srcs = ["test_kernel_panic.py"],
    resources = {
        ":create-kernel-panic=vmtest": "vmtest",
    },
    deps = [
        "//antlir:fs_utils",
        "//antlir/nspawn_in_subvol:common",
    ],
)

export_file(name = "resource.txt")

test_env_vars = {
    "dogsgo": "woof",
    "kitteh": "meow",
}

vm.python_unittest(
    name = "python",
    srcs = ["test_basic_vm.py"],
    env = test_env_vars,
    resources = {
        ":resource.txt": "resource",
    },
    vm_opts = vm.opts(
        ncpus = 4,
    ),
)

vm.cpp_unittest(
    name = "cpp",
    srcs = ["CppTest.cpp"],
    env = test_env_vars,
)

vm.python_unittest(
    name = "test-with-kernel-devel",
    srcs = ["test_kernel_devel.py"],
    vm_opts = vm.opts(
        devel = True,
    ),
)

KERNEL_ARTIFACTS = [
    "devel",
    "modules",
    "vmlinuz",
]

[
    buck_genrule(
        name = "0.01-{}-data".format(artifact),
        out = "out",
        cmd = "echo {} > $OUT".format(artifact),
        visibility = [],
    )
    for artifact in KERNEL_ARTIFACTS
]

[
    image.layer(
        name = "0.01-{}".format(artifact),
        features = [
            image.install(
                ":0.01-{}-data".format(artifact),
                "/data",
            ),
        ],
        visibility = [],
    )
    for artifact in KERNEL_ARTIFACTS
]

python_unittest(
    name = "test-kernel-artifacts",
    srcs = ["test_kernel_artifacts.py"],
    resources = {
        make_kernel_artifacts("0.01"): "kernel-artifacts",
    },
    deps = [
        "//antlir/vm:kernel_artifacts_t",
    ],
)
