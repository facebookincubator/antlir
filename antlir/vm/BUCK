load("//antlir/bzl:constants.bzl", "REPO_CFG")
load("//antlir/bzl:image.bzl", "image")
load("//antlir/bzl:oss_shim.bzl", "buck_genrule", "export_file", "python_library", "python_unittest", "third_party")
load("//antlir/bzl:vm.bzl", "vm")

# qemu roms to provide
roms = [
    "kvmvapic.bin",
    "linuxboot_dma.bin",
    "vgabios-stdvga.bin",
    "efi-virtio.rom",
]

buck_genrule(
    name = "roms",
    out = ".",
    cmd = " && ".join([
        "cp $(location {}) $OUT/".format(
            third_party.library(
                "qemu",
                "share/qemu/{}".format(rom),
            ),
        )
        for rom in roms
    ]),
)

python_library(
    name = "common",
    srcs = [
        "common.py",
    ],
)

# This is the client ssh key used to connect to the virtual machine with SSH
export_file(
    name = "ssh-privkey",
    src = "id_ecdsa",
)

# And the corresponding public key that is installed into the default image
export_file(
    name = "ssh-pubkey",
    src = "id_ecdsa.pub",
)

python_library(
    name = "vm",
    srcs = [
        "guest_conn.py",
        "vm.py",
    ],
    resources = {
        # The actual emulator binary
        third_party.library("qemu"): "qemu",
        # The bios to boot the emulator with
        third_party.library("qemu", "share/qemu/bios-256k.bin"): "bios",
        ":roms": "roms",
        ":ssh-privkey": "privkey",
    },
    deps = [
        ":share",
        ":tap",
        "//antlir:common",
        "//antlir:config",
        "//antlir:testlib_layer_resource",
        "//antlir:unshare",
        "//antlir/compiler/items:mount",
        "//antlir/nspawn_in_subvol:common",
    ],
)

python_library(
    name = "run",
    srcs = [
        "run.py",
    ],
    deps = [
        third_party.library(
            "click",
            platform = "python",
        ),
        ":common",
        ":vm",
    ],
)

python_library(
    name = "tap",
    srcs = ["tap.py"],
    deps = [
        "//antlir:unshare",
    ],
)

vm.python_unittest(
    name = "test-tap",
    srcs = ["test_tap.py"],
    deps = [":tap"],
)

# defines the `Share` dataclass for vm
python_library(
    name = "share",
    srcs = ["share.py"],
    deps = ["//antlir:fs_utils"],
)

export_file(
    name = "mount-generator",
    src = "mount-generator",
)

python_unittest(
    name = "test-share-generator",
    srcs = ["test_share_generator.py"],
    resources = {
        ":mount-generator": "mount-generator",
    },
    deps = [":share"],
)

python_library(
    name = "vmtest",
    srcs = [
        "vmtest.py",
    ],
    deps = [
        third_party.library(
            "click",
            platform = "python",
        ),
        ":common",
        ":share",
        ":vm",
        "//antlir:artifacts_dir",
        "//antlir:find_built_subvol",
        "//antlir:fs_utils",
    ],
)

# This will eventually be the default image used for vmtest both internally and
# externally, but for now is only used in the OSS antlir project.
image.layer(
    name = "default-image",
    parent_layer = REPO_CFG.build_appliance_default,
)

image.package(
    name = "default-image.btrfs",
)
