load("//antlir/antlir2/bzl:platform.bzl", "arch_select")
load("//antlir/antlir2/bzl/feature:defs.bzl", "feature")
load("//antlir/antlir2/bzl/image:defs.bzl", "image")
load("//antlir/antlir2/testing:image_diff_test.bzl", "image_diff_test")
load("//antlir/antlir2/testing:image_test.bzl", "image_python_test", "image_rust_test")
load("//antlir/bzl:build_defs.bzl", "buck_genrule", "buck_sh_test", "export_file", "python_binary", "rust_binary")
load("//antlir/bzl:constants.bzl", "REPO_CFG")

oncall("antlir")

image.layer(
    name = "base",
    features = [],
    flavor = "//antlir/antlir2/test_images:test-image-flavor",
)

image.layer(
    name = "install-file",
    features = [
        feature.install(
            src = "hello.txt",
            dst = "/hello",
            mode = "a+rw",
        ),
    ],
    parent_layer = ":base",
)

image_diff_test(
    name = "test-install-file",
    diff = "install-file.toml",
    diff_type = "file",
    layer = ":install-file",
)

image.layer(
    name = "install-text",
    features = [
        feature.install_text(
            dst = "/hello",
            mode = "a+rw",
            text = "Hello world!\n",
        ),
    ],
    parent_layer = ":base",
)

image_diff_test(
    name = "test-install-text",
    diff = "install-file.toml",
    diff_type = "file",
    layer = ":install-text",
)

buck_genrule(
    name = "genrule-dir",
    out = "out",
    cmd = """
      mkdir $OUT
      mkdir $OUT/foo
      touch $OUT/foo/bar
      echo "I am bar" > $OUT/foo/bar
    """,
)

image.layer(
    name = "install-dir",
    features = [
        feature.install(
            src = ":genrule-dir",
            dst = "/installed-dir/",
            mode = "a+rw,u+w",
        ),
    ],
    parent_layer = ":base",
)

image_diff_test(
    name = "test-install-dir",
    diff = "install-dir.toml",
    diff_type = "file",
    layer = ":install-dir",
)

image.layer(
    name = "child-image-install-dir",
    features = [
        # Create a subdir underneath a dir that exists
        # at least one level below the top of `:genrule-dir`.
        # This verifies that the dep graph is aware of sub-dirs
        # coming in implicitly.
        feature.ensure_subdirs_exist(
            into_dir = "/installed-dir/foo",
            subdirs_to_create = "child",
        ),
    ],
    parent_layer = ":install-dir",
)

image_diff_test(
    name = "test-child-image-install-dir",
    diff = "child-image-install-dir.toml",
    diff_type = "file",
    layer = ":child-image-install-dir",
)

buck_genrule(
    name = "genrule-dir-with-symlinks",
    out = "out",
    cmd = """
      mkdir $OUT
      ln -s /foo $OUT/bar
      ln -s baz $OUT/qux
    """,
)

image.layer(
    name = "install-dir-with-symlinks",
    features = [
        feature.install(
            src = ":genrule-dir-with-symlinks",
            dst = "/installed-dir/",
            mode = "a+rw,u+w",
        ),
    ],
    parent_layer = ":base",
)

image_diff_test(
    name = "test-install-dir-with-symlinks",
    diff = "install-dir-with-symlinks.toml",
    diff_type = "file",
    layer = ":install-dir-with-symlinks",
)

# use a fresh binaries that we know will not have already been materialized for
# some other use case
rust_binary(
    name = "true-rs",
    srcs = ["true.rs"],
    crate_root = "true.rs",
    unittests = False,
)

# hide that this is a binary using an export_file, otherwise antlir2 will apply
# the binary split debuginfo feature
export_file(
    name = "true-rs.unstripped",
    src = ":true-rs",
)

python_binary(
    name = "true-py",
    srcs = ["true.py"],
    main_module = "antlir.antlir2.test_images.install.true",
)

image.layer(
    name = "installed-binary",
    features = [
        feature.rpms_install(
            rpms = [
                "bash",
                "coreutils",
                "gdb",
                # @oss-disable
            ],
        ),
        feature.install(
            src = ":true-rs",
            dst = "/usr/bin/true-rs",
        ),
        feature.install(
            src = ":true-rs.unstripped",
            dst = "/usr/bin/true-rs.unstripped",
        ),
        feature.install(
            src = ":true-py",
            dst = "/usr/bin/true-py",
        ),
    ],
    flavor = "//antlir/antlir2/test_images:test-image-flavor",
)

image_python_test(
    name = "test-installed-binary",
    srcs = ["test_installed_binary.py"],
    layer = ":installed-binary",
    supports_static_listing = False,
)

export_file(
    name = "true.precompiled",
    src = arch_select(
        aarch64 = "true.precompiled.aarch64",
        x86_64 = "true.precompiled.x86_64",
    ),
)

buck_genrule(
    name = "deterministic-binary",
    cmd = "cp --reflink=auto $(location :true.precompiled) $OUT",
    executable = True,
)

# precompiled binary so that image_diff_test can be used to validate the split
# debug info support
image.layer(
    name = "deterministic-split-debuginfo",
    features = [
        feature.install(
            src = ":deterministic-binary",
            dst = "/true",
        ),
    ],
    parent_layer = ":base",
)

image_diff_test(
    name = "test-deterministic-split-debuginfo",
    diff = "deterministic-split-debuginfo.opt.toml",
    diff_type = "file",
    layer = ":deterministic-split-debuginfo",
) if not REPO_CFG.artifacts_require_repo else buck_sh_test(
    name = "test-deterministic-split-debuginfo",
    test = ":true-rs",
)

buck_genrule(
    name = "large-source",
    out = "large-source",
    cmd = """
        head -c 512M </dev/urandom >$OUT
    """,
    visibility = [],
)

image.layer(
    name = "reflink-test-layer",
    features = [
        feature.rpms_install(rpms = ["basesystem"]),
        feature.install(
            src = ":large-source",
            dst = "/installed",
        ),
    ],
    flavor = "//antlir/antlir2/test_images:test-image-flavor",
)

image_rust_test(
    name = "test-install-uses-reflink-copy",
    srcs = ["test_install_uses_reflink_copy.rs"],
    crate_root = "test_install_uses_reflink_copy.rs",
    layer = ":reflink-test-layer",
    deps = [
        "bitflags",
        "nix",
    ],
)
