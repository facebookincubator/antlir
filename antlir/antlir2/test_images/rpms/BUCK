load("//antlir/antlir2/bzl/feature:defs.bzl", "feature")
load("//antlir/antlir2/bzl/image:defs.bzl", "image")
load("//antlir/antlir2/testing:image_test.bzl", "image_sh_test")
load("//antlir/bzl:build_defs.bzl", "buck_sh_binary")
load("//antlir/rpm/dnf2buck:repo.bzl", "repo_set")
load(":defs.bzl", "test_foo_version_installed")

repo_set(
    name = "test-repo-set",
    repo_sets = select({
        "ovr_config//cpu:arm64": [
            "//bot_generated/antlir/rpm/fast_snapshot/by_flavor:centos9-aarch64",
        ],
        "ovr_config//cpu:x86_64": [
            "//bot_generated/antlir/rpm/fast_snapshot/by_flavor:centos9",
        ],
    }),
    repos = ["//antlir/antlir2/test_images/rpms/repo:test-repo"],
)

image.layer(
    name = "simple",
    dnf_available_repos = ":test-repo-set",
    features = [
        feature.rpms_install(
            rpms = [
                # the main rpm I am checking
                "foo",
                # this depends on 'foobar' and 'foobarbaz' should be removed when 'foobar' is
                "foobarbaz",
                # dependencies of the test
                "bash",
                "rpm",
            ],
        ),
    ],
    flavor = "//antlir/antlir2/test_images:test-image-flavor",
)

buck_sh_binary(
    name = "test-foo-version.sh",
    main = "test-foo-version.sh",
)

test_foo_version_installed(
    name = "simple-test",
    layer = ":simple",
    version = "3-1",
)

image.layer(
    name = "remove",
    dnf_available_repos = ":test-repo-set",
    features = [
        feature.rpms_remove_if_exists(rpms = [
            "foo",
            "foobar",
        ]),
    ],
    parent_layer = ":simple",
)

image_sh_test(
    name = "remove-test",
    layer = ":remove",
    test = "test-remove.sh",
)

image.layer(
    name = "local-rpm-file",
    features = [
        feature.rpms_install(rpms = ["//antlir/antlir2/test_images/rpms/repo:foo-2.rpm"]),
    ],
    flavor = "//antlir/antlir2/test_images:test-image-flavor",
    parent_layer = ":simple",
)

test_foo_version_installed(
    name = "local-rpm-file-test",
    layer = ":local-rpm-file",
    version = "2-1",
)
