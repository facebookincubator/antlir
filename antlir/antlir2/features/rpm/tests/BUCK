load("//antlir/antlir2/bzl:configured_alias.bzl", "antlir2_configured_alias")
load("//antlir/antlir2/bzl/feature:defs.bzl", "feature")
load("//antlir/antlir2/os:oses.bzl", "OSES")
load("//antlir/antlir2/package_managers/dnf/rules:repo.bzl", "repo_set")
load("//antlir/antlir2/testing:image_test.bzl", "image_python_test", "image_sh_test")
load("//antlir/bzl:build_defs.bzl", "alias", "buck_genrule", "python_binary")
load("//antlir/bzl:internal_external.bzl", "internal_external")
load(":defs.bzl", "expected_t", "test_rpms")

oncall("antlir")

repo_set(
    name = "test-repo-set",
    compatible_with = [
        os.select_key
        for os in OSES
    ],
    repo_sets = select({"DEFAULT": []} | {
        os.select_key: internal_external(
            fb = ["//bot_generated/antlir/rpm/fast_snapshot/by_flavor:" + os.name],
            oss = ["generated//snapshot/rpm/{}:repos".format(os.name)],
        )
        for os in OSES
    }),
    repos = ["//antlir/antlir2/features/rpm/tests/repo:test-repo"],
    visibility = [
        "//antlir/antlir2/bzl/feature/tests/rpm:",
        "//antlir/antlir2/features/facebook/chef_solo/...",
        "//antlir/antlir2/testing/tests:",
    ],
)

feature.new(
    name = "test-deps",
    features = [
        feature.rpms_install(rpms = [
            "dnf",
            "python3",
            "rpm",
            # @oss-disable
        ]),
    ],
    visibility = [
        "//antlir/antlir2/features/rpm/tests/...",
    ],
)

simple = test_rpms(
    name = "simple",
    also_eln_dnf5 = True,
    expected = expected_t(
        installed = [
            "foo-2-1",
            "foobar-2-1",
            "foobarbaz-2-1",
            "foo-epoch-3:1-1",
        ],
        installed_not_userinstalled = ["foobar"],
        not_installed = ["foo-recommends-me"],
        userinstalled = [
            "foo",
            "foobarbaz",
            "foo-epoch",
        ],
    ),
    features = [
        feature.rpms_install(
            rpms = [
                # the main rpm I am checking
                "foo-2",
                # this depends on 'foobar' and 'foobarbaz' should be removed when 'foobar' is
                "foobarbaz",
                # this has a nonzero epoch
                "foo-epoch-3:1-1.noarch",
            ],
        ),
        ":test-deps",
    ],
)

python_binary(
    name = "test-installed-rpms",
    srcs = ["test_installed_rpms.py"],
    main_module = "antlir.antlir2.features.rpm.tests.test_installed_rpms",
    visibility = [],
)

test_rpms(
    name = "remove",
    also_eln_dnf5 = True,
    expected = expected_t(not_installed = [
        "foo",
        # 'foobar' is not explicitly installed, so removing 'foo' will remove it
        "foobar",
        "foobarbaz",
    ]),
    features = [
        feature.rpms_remove_if_exists(rpms = [
            "foo",
            "foobarbaz",
        ]),
    ],
    parent_layer = simple,
)

test_rpms(
    name = "autoremove",
    also_eln_dnf5 = True,
    expected = expected_t(
        installed = ["foo"],
        not_installed = [
            # 'foobar' is not explicitly installed, so removing 'foobarbaz' will
            # remove it since the only thing depending on it is 'foobarbaz'
            "foobar",
            "foobarbaz",
        ],
    ),
    features = [
        feature.rpms_remove_if_exists(rpms = [
            "foobarbaz",
        ]),
    ],
    parent_layer = simple,
)

test_rpms(
    name = "reinstall-dep-as-userinstalled-prevents-autoremove",
    also_eln_dnf5 = True,
    expected = expected_t(
        installed = [
            "foo",
            "foobar",
        ],
        not_installed = [
            # 'foobar' is not explicitly installed, so removing 'foobarbaz' will
            # remove it since the only thing depending on it is 'foobarbaz'
            "foobarbaz",
        ],
        userinstalled = [
            "foo",
            "foobar",
        ],
    ),
    features = [
        feature.rpms_install(rpms = [
            # "foobar" has already been implicitly installed. But now we'll
            # explicitly install it to make sure it gets marked as installed
            # and isn't isn't implicitly removed.
            "foobar",
        ]),
        feature.rpms_remove_if_exists(rpms = [
            "foobarbaz",
        ]),
    ],
    parent_layer = simple,
)

test_rpms(
    name = "upgrade-by-installing-nevra",
    also_eln_dnf5 = True,
    expected = expected_t(
        installed = ["foo-3"],
        not_installed = ["foo-2"],
    ),
    features = [
        feature.rpms_install(rpms = ["foo-3"]),
    ],
    parent_layer = simple,
)

test_rpms(
    name = "upgrade",
    also_eln_dnf5 = True,
    dnf_versionlock = "//antlir:empty.json",
    expected = expected_t(
        installed = ["foo-3"],
        not_installed = ["foo-2"],
    ),
    features = [
        feature.rpms_upgrade(rpms = ["foo"]),
    ],
    parent_layer = simple,
)

test_rpms(
    name = "upgrade-respects-versionlock",
    also_eln_dnf5 = True,
    dnf_versionlock = ":versionlock.json",
    expected = expected_t(
        installed = ["foo-2"],
        not_installed = ["foo-3"],
    ),
    features = [
        feature.rpms_upgrade(rpms = ["foo"]),
    ],
    parent_layer = simple,
)

test_rpms(
    name = "upgrade-not-already-installed",
    also_eln_dnf5 = True,
    expected = expected_t(
        installed = [
            "foo-3-1",
        ],
    ),
    features = [
        feature.rpms_upgrade(rpms = ["foo"]),
        ":test-deps",
    ],
)

test_rpms(
    name = "upgrade-not-already-installed-file",
    also_eln_dnf5 = True,
    expected = expected_t(
        installed = [
            "foo-2-1",
        ],
    ),
    features = [
        feature.rpms_upgrade(rpms = ["//antlir/antlir2/features/rpm/tests/repo:foo-2-1.noarch"]),
        ":test-deps",
    ],
)

test_rpms(
    name = "downgrade",
    also_eln_dnf5 = True,
    expected = expected_t(
        installed = ["foo-1"],
        not_installed = ["foo-2"],
    ),
    features = [
        feature.rpms_install(rpms = ["foo-1"]),
    ],
    parent_layer = simple,
)

test_rpms(
    name = "reinstalled-package-userinstalled",
    also_eln_dnf5 = True,
    expected = expected_t(
        userinstalled = ["foobar"],
    ),
    features = [
        feature.rpms_install(
            rpms = [
                # This is already installed, but will not show as user-installed
                # since it came from a dependency.
                # If we install it now explicitly, dnf will no-op, but it should
                # mark it as user-installed for later safety.
                "foobar",
            ],
        ),
    ],
    parent_layer = simple,
)

test_rpms(
    name = "userinstalled-package-upgraded-as-dep",
    also_eln_dnf5 = True,
    expected = expected_t(
        userinstalled = ["foo"],
    ),
    features = [
        feature.rpms_install(
            rpms = [
                # This will trigger an upgrade of 'foo' which should still be
                # recorded as a userinstalled package
                "foobar-3",
            ],
        ),
    ],
    parent_layer = simple,
)

test_rpms(
    name = "userinstalled-package-reinstalled-as-dep",
    also_eln_dnf5 = True,
    expected = expected_t(
        userinstalled = ["foo"],
    ),
    features = [
        feature.rpms_install(
            rpms = [
                # This may trigger a "reinstall" (no-oped) of 'foo' which should
                # still be recorded as a userinstalled package
                "foobar-3",
            ],
        ),
    ],
    parent_layer = simple,
)

test_rpms(
    name = "local-rpm-file",
    also_eln_dnf5 = True,
    expected = expected_t(installed = ["foo-2-1"]),
    features = [
        feature.rpms_install(rpms = [
            "//antlir/antlir2/features/rpm/tests/repo:foo-2-1.noarch",
        ]),
        ":test-deps",
    ],
)

test_rpms(
    name = "subjects-src",
    also_eln_dnf5 = True,
    expected = expected_t(installed = ["foo-2-1"]),
    features = [
        feature.rpms_install(subjects_src = "subjects-src"),
        ":test-deps",
    ],
)

test_rpms(
    name = "remove-file-from-rpm",
    also_eln_dnf5 = True,
    expected = expected_t(installed = ["foo"]),
    features = [
        feature.rpms_install(
            rpms = ["foo"],
        ),
        ":test-deps",
        feature.remove(
            must_exist = True,
            path = "/foo",
        ),
    ],
)

alias(
    name = "flavor-versionlock",
    actual = select({
        os.select_key: os.flavor + "[default_versionlock]"
        for os in OSES
    }),
    compatible_with = [
        os.select_key
        for os in OSES
    ],
)

buck_genrule(
    name = "versionlock.json",
    out = "versionlock.json",
    cmd = """
        jq '.foo = "2-1"' < $(location :flavor-versionlock) > $OUT
    """,
    compatible_with = [
        os.select_key
        for os in OSES
    ],
)

test_rpms(
    name = "versionlock",
    also_eln_dnf5 = True,
    dnf_versionlock = ":versionlock.json",
    expected = expected_t(installed = ["foo-2-1"]),
    features = [
        feature.rpms_install(rpms = [
            "foo",  # test rpm
        ]),
        ":test-deps",
    ],
)

test_rpms(
    name = "versionlocked-dependency",
    also_eln_dnf5 = True,
    dnf_versionlock = ":versionlock.json",
    expected = expected_t(installed = ["foo-2-1"]),
    features = [
        feature.rpms_install(rpms = [
            # Only explicitly install 'foobar', which depends on 'foo'.
            # Without versionlock (or if versionlock only worked on
            # explicitly-requested packages), this would pull in 'foo-3-1', but
            # when versionlock is applied to dependencies 'foo-2-1' will end up
            # being installed.
            "foobar",
        ]),
        ":test-deps",
    ],
)

test_rpms(
    name = "nevra-overrides-versionlock",
    also_eln_dnf5 = True,
    dnf_versionlock = ":versionlock.json",
    expected = expected_t(installed = ["foo-3-1"]),
    features = [
        feature.rpms_install(rpms = [
            "foo-3-1",
        ]),
        ":test-deps",
    ],
)

test_rpms(
    name = "versionlock-extend",
    also_eln_dnf5 = True,
    dnf_versionlock = ":versionlock.json",
    dnf_versionlock_extend = {
        "foo": "1-1",
    },
    expected = expected_t(installed = ["foo-1-1"]),
    features = [
        feature.rpms_install(rpms = [
            "foo",
        ]),
        ":test-deps",
    ],
)

protected_installed = test_rpms(
    name = "install-protected",
    also_eln_dnf5 = False,
    expected = expected_t(installed = ["dnf"]),
    features = [
        feature.rpms_install(rpms = [
            "dnf",
        ]),
        ":test-deps",
    ],
)

test_rpms(
    name = "remove-protected",
    also_eln_dnf5 = False,
    expected = expected_t(not_installed = ["dnf"]),
    features = [
        feature.rpms_remove_if_exists(rpms = [
            "dnf",
        ]),
    ],
    parent_layer = protected_installed,
)

image_sh_test(
    name = "test-history",
    layer = simple,
    test = "test-history.sh",
)

test_rpms(
    name = "allowlisted-failing-postscripts",
    # the newer version of dnf4 in eln fails the entire transaction if
    # scriptlets fail
    also_eln_dnf5 = False,
    expected = expected_t(installed = [
        "antlir2-failing-postscripts",
    ]),
    features = [
        feature.rpms_install(rpms = [
            "antlir2-failing-postscripts",
        ]),
        ":test-deps",
    ],
)

test_rpms(
    name = "postscript-tmpdir",
    also_eln_dnf5 = True,
    expected = expected_t(installed = [
        "antlir2-postscript-tmpdir",
    ]),
    features = [
        feature.rpms_install(rpms = [
            "antlir2-postscript-tmpdir",
            "/usr/bin/umount",  # for the test
        ]),
        ":test-deps",
    ],
)

image_python_test(
    name = "test-postscript-tmpdir-not-left-behind",
    srcs = ["test_postscript_tmpdir_not_left_behind.py"],
    layer = ":postscript-tmpdir--layer",
)

antlir2_configured_alias(
    name = "test-postscript-tmpdir-not-left-behind-dnf5",
    actual = ":test-postscript-tmpdir-not-left-behind",
    default_os = "eln",
)

test_rpms(
    name = "postscript-proc-self",
    also_eln_dnf5 = True,
    expected = expected_t(installed = [
        "antlir2-postscript-proc-self",
    ]),
    features = [
        feature.rpms_install(rpms = [
            "antlir2-postscript-proc-self",
        ]),
        ":test-deps",
    ],
)

image_python_test(
    name = "test-db-backend",
    srcs = ["test_db_backend.py"],
    layer = simple,
)

test_binaries_with_file_capabilities_layer = test_rpms(
    name = "test-binaries-with-file-capabilities-layer",
    also_eln_dnf5 = True,
    expected = expected_t(installed = [
        "antlir2-with-capability",
    ]),
    features = [feature.rpms_install(rpms = [
        "libcap",  # getcap cli
        "antlir2-with-capability",
    ])],
    parent_layer = simple,
)

image_python_test(
    name = "test-binaries-with-file-capabilities",
    srcs = ["test_binaries_with_file_capabilities.py"],
    layer = test_binaries_with_file_capabilities_layer,
)

test_rpms(
    name = "explicitly-install-excluded-rpm",
    also_eln_dnf5 = True,
    dnf_excluded_rpms = ["antlir2-excluded"],  # this would normally come from the flavor
    expected = expected_t(installed = [
        "antlir2-excluded",
    ]),
    features = [
        feature.rpms_install(rpms = [
            "antlir2-excluded",
        ]),
        ":test-deps",
    ],
)
