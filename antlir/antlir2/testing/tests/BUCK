load("//antlir/antlir2/bzl/feature:defs.bzl", "feature")
load("//antlir/antlir2/bzl/image:defs.bzl", "image")
load("//antlir/antlir2/testing:image_rpms_test.bzl", "image_test_rpm_integrity", "image_test_rpm_names")
load("//antlir/antlir2/testing:image_test.bzl", "image_cpp_test", "image_python_test", "image_rust_test", "image_sh_test")
load("//antlir/antlir2/testing:test_that_should_fail.bzl", "sh_test_that_should_fail", "test_that_should_fail")
load("//antlir/bzl:systemd.bzl", "systemd")

image.layer(
    name = "base",
    features = [
        feature.rpms_install(rpms = [
            "systemd",
            # @oss-disable
        ]),
        systemd.install_unit("slow-unit.service"),
        systemd.enable_unit(
            "slow-unit.service",
            target = "default.target",
        ),
    ],
    flavor = "//antlir/antlir2/test_images:test-image-flavor",
)

[
    [
        image_cpp_test(
            name = "test-cpp" + ("-booted" if boot else "") + ("-requires-units" if boot == "wait-default" else ""),
            srcs = ["test.cpp"],
            boot = bool(boot),
            boot_requires_units = ["default.target"] if boot == "wait-default" else [],
            env = {
                "ANTLIR2_TEST": "1",
                "BOOT": str(boot),
            },
            layer = ":base",
        ),
        image_python_test(
            name = "test-py" + ("-booted" if boot else "") + ("-requires-units" if boot == "wait-default" else ""),
            srcs = ["test.py"],
            boot = bool(boot),
            boot_requires_units = ["default.target"] if boot == "wait-default" else [],
            env = {
                "ANTLIR2_TEST": "1",
                "BOOT": str(boot),
            },
            layer = ":base",
            supports_static_listing = False,  #TODO: disable static listing for underlying test in antlir2 macro
        ),
        image_rust_test(
            name = "test-rs" + ("-booted" if boot else "") + ("-requires-units" if boot == "wait-default" else ""),
            srcs = ["test.rs"],
            boot = bool(boot),
            boot_requires_units = ["default.target"] if boot == "wait-default" else [],
            crate = "test_rs",
            crate_root = "test.rs",
            env = {
                "ANTLIR2_TEST": "1",
                "BOOT": str(boot),
            },
            layer = ":base",
        ),
        image_sh_test(
            name = "test-sh" + ("-booted" if boot else "") + ("-requires-units" if boot == "wait-default" else ""),
            boot = bool(boot),
            boot_requires_units = ["default.target"] if boot == "wait-default" else [],
            env = {
                "ANTLIR2_TEST": "1",
                "BOOT": str(boot),
            },
            layer = ":base",
            test = "test.sh",
        ),
    ]
    for boot in [
        False,
        True,
        "wait-default",
    ]
]

image.layer(
    name = "foo-rpms",
    dnf_available_repos = "//antlir/antlir2/test_images/rpms:test-repo-set",
    features = [
        feature.rpms_install(rpms = ["foobarbaz"]),
    ],
    flavor = "//antlir/antlir2/test_images:test-image-flavor",
)

image_test_rpm_names(
    name = "test-foo-rpm-names",
    src = "foo-rpms.txt",
    layer = ":foo-rpms",
)

image_test_rpm_integrity(
    name = "test-foo-rpm-integrity",
    layer = ":foo-rpms",
)

image.layer(
    name = "foo-rpms-integrity-violated",
    features = [
        feature.remove(path = "/foo/bar/baz"),
    ],
    parent_layer = ":foo-rpms",
)

test_that_should_fail(
    name = "test-foo-rpm-integrity-removed-file",
    layer = ":foo-rpms-integrity-violated",
    stdout_re = "(?s)FileIntegrity.*\"\\/foo\\/bar\\/baz\".*Missing",
    test_rule = image_test_rpm_integrity,
)

sh_test_that_should_fail(
    name = "test-that-should-fail",
    stderr_re = "^this is some stderr of a test that is expected to fail\n$",
    stdout_re = "^this is some stdout of a test that is expected to fail\n$",
    test = "failing_test.sh",
)

test_that_should_fail(
    name = "image-test-that-should-fail",
    layer = ":base",
    stderr_re = "^this is some stderr of a test that is expected to fail\n$",
    stdout_re = "^this is some stdout of a test that is expected to fail\n$",
    test = "failing_test.sh",
    test_rule = image_sh_test,
)

test_that_should_fail(
    name = "booted-image-test-that-should-fail",
    boot = True,
    layer = ":base",
    stderr_re = "^this is some stderr of a test that is expected to fail\n$",
    stdout_re = "^this is some stdout of a test that is expected to fail\n$",
    test = "failing_test.sh",
    test_rule = image_sh_test,
)
