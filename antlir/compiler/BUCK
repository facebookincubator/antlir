# NB: There is no binary here because the image compiler does not get built
# by Buck, it is executed in-place using the system Python.

load("//antlir/bzl:constants.bzl", "REPO_CFG")
load("//antlir/bzl:flavor_helpers.bzl", "flavor_helpers")
load("//antlir/bzl:layer_resource.bzl", "LAYER_SLASH_ENCODE", "layer_resource")
load("//antlir/bzl:oss_shim.bzl", "python_library", "python_unittest")
load(":defs.bzl", "READ_MY_DOC_image_feature_target", "TEST_IMAGE_PREFIX", "image_feature_python_unittest")

python_library(
    name = "subvolume_on_disk",
    srcs = ["subvolume_on_disk.py"],
    deps = ["//antlir:fs_utils"],
)

python_unittest(
    name = "test-subvolume-on-disk",
    srcs = ["tests/test_subvolume_on_disk.py"],
    needed_coverage = [(
        100,
        ":subvolume_on_disk",
    )],
    deps = [":subvolume_on_disk"],
)

python_library(
    name = "procfs_serde",
    srcs = ["procfs_serde.py"],
    deps = ["//antlir:fs_utils"],
)

python_unittest(
    name = "test-procfs-serde",
    srcs = ["tests/test_procfs_serde.py"],
    needed_coverage = [(
        100,
        ":procfs_serde",
    )],
    deps = [
        ":procfs_serde",
        "//antlir:subvol_utils",
        "//antlir:testlib_subvol_helpers",
    ],
)

python_library(
    name = "requires_provides",
    srcs = ["requires_provides.py"],
    deps = ["//antlir:fs_utils"],
)

python_unittest(
    name = "test-requires-provides",
    srcs = ["tests/test_requires_provides.py"],
    needed_coverage = [(
        100,
        ":requires_provides",
    )],
    deps = [":requires_provides"],
)

python_library(
    name = "dep_graph",
    srcs = ["dep_graph.py"],
    # NB: Don't use `all-items` here, just use the ones we need.
    deps = [
        "//antlir:fs_utils",
        "//antlir/compiler/items:genrule_layer",
        "//antlir/compiler/items:make_subvol",
        "//antlir/compiler/items:phases_provide",
    ],
)

python_unittest(
    name = "test-dep-graph",
    srcs = ["tests/test_dep_graph.py"],
    needed_coverage = [(
        100,
        ":dep_graph",
    )],
    deps = [
        ":dep_graph",
        "//antlir:subvol_utils",
        "//antlir/compiler/items:ensure_dirs_exist",
        "//antlir/compiler/items:items",
    ],
)

python_library(
    name = "items_for_features",
    srcs = ["items_for_features.py"],
    visibility = [
        "//antlir/rpm/replay/...",
        "//tupperware/cm/antlir/...",
    ],
    deps = ["//antlir/compiler/items:all-items"],
)

python_library(
    name = "sample_items",
    srcs = ["tests/sample_items.py"],
    deps = ["//antlir/compiler/items:all-items"],
)

# Transitive deps of `tests:feature_kitchen_sink` --

TEST_IMAGE_FEATURE_TRANSITIVE_DEPS = [
    READ_MY_DOC_image_feature_target("feature_dirs"),
    READ_MY_DOC_image_feature_target("feature_bad_dir"),
    READ_MY_DOC_image_feature_target("feature_bad_dir_mode_mismatch"),
    READ_MY_DOC_image_feature_target("feature_symlinks"),
    READ_MY_DOC_image_feature_target("feature_tar_and_rpms"),
    "rpm-test-cheese-2-1.rpm",
    READ_MY_DOC_image_feature_target("feature_mount"),
    "print-ok",
    "dir-print-ok",
    "dir-with-script",
    READ_MY_DOC_image_feature_target("feature_install_files"),
    READ_MY_DOC_image_feature_target("feature_kitchen_sink"),
    "hello_world_base",
    "hello_world.tar",
    "shadow_me",
] + ([
    "dir-print-ok__install_buck_runnable_wrap_source-dir-print-ok__aSw-wLLqmDSnGfAZQzPs",
    "print-ok__install_buck_runnable_wrap_source-print-ok__tZD0yB-oR8iaa7Q8gX1u",
] if REPO_CFG.artifacts_require_repo else [])

image_feature_python_unittest(
    name = "test-image-feature",
    srcs = ["tests/test_image_feature.py"],
    # I cannot get coverage for `image_feature.py` here, because that is
    # executed by the Buck Python runtime.  I could conceivably shim all
    # that out, but it seems entirely not worth the trouble.
    needed_coverage = [(
        100,
        ":items_for_features",
    )],
    test_image_feature_transitive_deps = TEST_IMAGE_FEATURE_TRANSITIVE_DEPS,
    deps = [
        ":dep_graph",
        ":items_for_features",
        "//antlir:fs_utils",
        "//antlir:subvol_utils",
    ],
)

python_library(
    name = "compiler",
    srcs = ["compiler.py"],
    deps = [
        ":dep_graph",
        ":items_for_features",
        ":subvolume_on_disk",
        "//antlir:cli",
        "//antlir:fs_utils",
    ],
)

image_feature_python_unittest(
    name = "test-compiler",
    srcs = ["tests/test_compiler.py"],
    needed_coverage = [(
        100,
        ":compiler",
    )],
    resources = {
        layer_resource(
            TEST_IMAGE_PREFIX + "build_appliance_testing",
        ): "tests/test-build-appliance",
    },
    test_image_feature_transitive_deps = TEST_IMAGE_FEATURE_TRANSITIVE_DEPS,
    deps = [
        ":compiler",
        "//antlir:testlib_layer_resource",
    ],
)

IMAGE_LAYER_DEPS = [
    "genrule-layer",
    "genrule-layer-with-mounts",
    "layer-with-non-default-snapshot-rpm",
    "installed-files",
    "cloned-files",
    "child/layer",
    "parent_layer",
    "base_cheese_layer",
    "older_cheese_layer",
    "newer_cheese_layer",
    "reinstall_cheese_layer",
    "hello_world_base",
    "create_ops",
    "create_ops-from-dir",
    "create_ops-from-layer",
    "create_ops-alias",
] + [
    "validates-{}-build-appliance".format(i)
    for i in flavor_helpers.get_rpm_installers_supported()
]

python_unittest(
    name = "test-image-layer",
    srcs = ["tests/test_image_layer.py"],
    # Python `deps` don't work as you'd expect in fbcode -- they have to be
    # other `python_*` rules.  `cpp_deps` is the kitchen sink?
    cpp_deps = [
        # Buck bug: `env` does not create a dependency; do it explicitly.
        TEST_IMAGE_PREFIX + t
        for t in IMAGE_LAYER_DEPS
    ],
    # NB: We could replace this with `resources` if this breaks, but while
    # the `env` thing works, the effort isn't worth the reward.  One upside
    # of ditching `env` is that the test will start working stand-alone
    # (running `test-image-layer#binary.par`) without needing `buck test`.
    resources = {
        layer_resource(
            TEST_IMAGE_PREFIX + t,
        ): "tests/test_image_layer_path_to_" + t.replace("/", LAYER_SLASH_ENCODE)
        for t in IMAGE_LAYER_DEPS
    },
    # As with `test-image-feature`, we cannot get `image_layer.py` coverage.
    deps = [
        ":procfs_serde",
        ":sample_items",
        "//antlir:artifacts_dir",
        "//antlir:config",
        "//antlir:find_built_subvol",
        "//antlir:testlib_flavor_helpers",
        "//antlir:testlib_layer_resource",
        "//antlir:testlib_subvol_helpers",
        "//antlir/btrfs_diff:testlib_demo_sendstreams",
    ],
)

# This isn't explicitly covering any single library, so no coverage claims.
python_unittest(
    name = "test-add-remove-conflict",
    srcs = ["tests/test_add_remove_conflict.py"],
    resources = {
        TEST_IMAGE_PREFIX + READ_MY_DOC_image_feature_target(
            "feature_addremove_conflict_add",
        ): "tests/feature_add",
        TEST_IMAGE_PREFIX + READ_MY_DOC_image_feature_target(
            "feature_addremove_conflict_remove",
        ): "tests/feature_remove",
        TEST_IMAGE_PREFIX + READ_MY_DOC_image_feature_target(
            "feature_addremove_conflict_both",
        ): "tests/feature_both",
        layer_resource(
            TEST_IMAGE_PREFIX + "addremove_conflict_child",
        ): "tests/child",
        layer_resource(
            TEST_IMAGE_PREFIX + "addremove_conflict_parent",
        ): "tests/parent",
    },
    deps = [
        ":compiler",
        "//antlir:subvol_utils",
        "//antlir:testlib_flavor_helpers",
        "//antlir:testlib_layer_resource",
        "//antlir:testlib_subvol_helpers",
    ],
)
