load("//antlir/bzl:flavor_helpers.bzl", "flavor_helpers")
load("//antlir/bzl:image.bzl", "image")
load("//antlir/bzl:oss_shim.bzl", "third_party")
load("//antlir/bzl/image/feature:defs.bzl", "feature")
load("//antlir/bzl/linux:defs.bzl", "linux")

# The `busybox.install()` feature requires a source image.layer
# from which to clone `/busybox` from.  These targets produce
# image layers with `busybox` installed at `/`, one built from
# the default Build Appliance and the other built from a
# `third_party.library` target.
image.layer(
    name = "busybox-src-from-ba",
    features = [
        image.clone(
            flavor_helpers.default_flavor_build_appliance,
            "/usr/sbin/busybox",
            "/busybox",
        ),
    ],
)

image.layer(
    name = "busybox-src-from-third-party",
    features = [
        feature.install(
            third_party.library("busybox", "bin/busybox"),
            "/busybox",
        ),
    ],
)

# Now use the respective busybox source layers to construct
# test linux layers.
image.layer(
    name = "test-linux-busybox-from-ba",
    features = [
        linux.filesystem.install(),
        linux.busybox.install(
            src = ":busybox-src-from-ba",
        ),
    ],
)

image.layer(
    name = "test-linux-busybox-from-third-party",
    features = [
        linux.filesystem.install(),
        linux.busybox.install(
            src = ":busybox-src-from-third-party",
        ),
    ],
)

# Time tests
image.layer(
    name = "test-layer-timezone-utc",
    parent_layer = flavor_helpers.default_flavor_build_appliance,
    features = [linux.time.timezone(zone = "UTC")],
)

image.python_unittest(
    name = "test-timezone-utc",
    env = {
        "ANTLIR_TEST_EXPECTED_TIMEZONES": "UTC",
    },
    srcs = ["test_time.py"],
    layer = ":test-layer-timezone-utc",
)

image.layer(
    name = "test-layer-timezone-us-pacific",
    parent_layer = flavor_helpers.default_flavor_build_appliance,
    features = [linux.time.timezone(zone = "US/Pacific")],
)

image.python_unittest(
    name = "test-timezone-us-pacific",
    env = {
        "ANTLIR_TEST_EXPECTED_TIMEZONES": "PDT PST",
    },
    srcs = ["test_time.py"],
    layer = ":test-layer-timezone-us-pacific",
)
