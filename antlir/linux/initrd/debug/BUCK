load("@bazel_skylib//lib:new_sets.bzl", "sets")
load("@bazel_skylib//lib:paths.bzl", "paths")
load("//antlir/bzl:image.bzl", "image")
load("//antlir/bzl:oss_shim.bzl", "kernel_get")
load("//antlir/bzl:systemd.bzl", "systemd", SYSTEMD_PROVIDER_ROOT = "PROVIDER_ROOT")
load("//antlir/bzl/linux:busybox.bzl", "DEFAULT_APPLETS")
load("//antlir/bzl/linux:defs.bzl", "linux")
load("//antlir/linux:defs.bzl", "antlir_linux_build_opts")

DEPS_TARGET = "//antlir/linux/initrd:deps"

# Some systemd units and binaries that are only useful for debugging and not
# during normal boots
systemd_debug = [
    # We don't have to use the extractor here, since the libraries loaded by
    # the systemd binaries that we want will already be present in the base
    # initrd.
    image.clone(DEPS_TARGET, "/usr/bin/systemd-analyze", "/usr/bin/systemd-analyze"),
    image.ensure_subdirs_exist(
        "/usr/lib",
        paths.relativize(SYSTEMD_PROVIDER_ROOT, "/usr/lib"),
    ),
    image.clone(
        DEPS_TARGET,
        paths.join(SYSTEMD_PROVIDER_ROOT, "debug-shell.service"),
        paths.join(SYSTEMD_PROVIDER_ROOT, "debug-shell.service"),
    ),
]

image.layer(
    name = "debug-append",
    features = [
        linux.filesystem.install(),
        linux.busybox.install(
            DEPS_TARGET,
            # /usr/bin/mount is implemented in antlirctl
            sets.difference(
                DEFAULT_APPLETS,
                sets.make(["mount"]),
            ),
            src_path = "/usr/sbin/busybox",
        ),
        systemd_debug,
        systemd.enable_unit("debug-shell.service"),
        systemd.install_dropin("debug-shell.conf", "debug-shell.service"),
        systemd.enable_unit("debug-shell.service"),
        systemd.install_dropin("wait-for-debug-shell.conf", "initrd-cleanup.service"),
    ],
    flavor_config_override = antlir_linux_build_opts(),
)

image.package(
    name = "debug-append.cpio.gz",
    layer = ":debug-append",
    visibility = [
        kernel_get.base_target + "/...",
        "//antlir/linux/...",
        "//scripts/dschatzberg/vm/...",
    ],
    build_appliance = antlir_linux_build_opts().build_appliance,
)
