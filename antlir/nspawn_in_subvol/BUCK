# Future: we should eventually add per-library unit test coverage, as
# appropriate, while also retaining integration test coverage.

load("@bazel_skylib//lib:shell.bzl", "shell")
load("//antlir/bzl:constants.bzl", "REPO_CFG")
load("//antlir/bzl:image.bzl", "image")
load("//antlir/bzl:layer_resource.bzl", "layer_resource")
load("//antlir/bzl:oss_shim.bzl", "buck_genrule", "python_binary", "python_library", "python_unittest")
load("//antlir/compiler:defs.bzl", "TEST_IMAGE_PREFIX")

# Gets test coverage via `:test-run`
python_library(
    name = "args",
    srcs = ["args.py"],
    deps = [
        "//antlir:find_built_subvol",
        "//antlir:fs_utils",
    ],
)

python_library(
    name = "common",
    srcs = ["common.py"],
    deps = ["//antlir:fs_utils"],
)

python_unittest(
    name = "test-common",
    srcs = ["tests/test_common.py"],
    needed_coverage = [(100, ":common")],
)

# Gets test coverage via `:test-run`
python_library(
    name = "cmd",
    srcs = ["cmd.py"],
    deps = [
        ":args",
        "//antlir:config",
        "//antlir:find_built_subvol",
        "//antlir:send_fds_and_run",
        "//antlir:testlib_temp_subvolumes",
        "//antlir/compiler:procfs_serde",
        "//antlir/compiler/items:common",
        "//antlir/compiler/items:mount",
    ],
)

# Gets test coverage via `plugins:test-yum-dnf-versionlock`
python_library(
    name = "plugin_hooks",
    srcs = ["plugin_hooks.py"],
    deps = [
        ":args",
        ":cmd",
        "//antlir/nspawn_in_subvol/plugins:plugins",
    ],
)

# Gets test coverage via `:test-run`
python_library(
    name = "booted",
    srcs = ["booted.py"],
    deps = [
        ":args",
        ":cmd",
        ":common",
        ":plugin_hooks",
        "//antlir:common",
        "//antlir:send_fds_and_run",
    ],
)

# Gets test coverage via `:test-run`
python_library(
    name = "non_booted",
    srcs = ["non_booted.py"],
    deps = [
        ":args",
        ":cmd",
        ":common",
        ":plugin_hooks",
    ],
)

python_library(
    name = "ba_runner",
    srcs = ["ba_runner.py"],
)

python_binary(
    name = "run",
    srcs = ["run.py"],
    main_module = "antlir.nspawn_in_subvol.run",
    # Ensures we can read resources in @mode/opt. "xar" cannot work because
    # `root` cannot access the content of unprivileged XARs.
    par_style = "zip",
    deps = [
        ":args",
        ":booted",
        ":cmd",
        ":non_booted",
        "//antlir:common",
        "//antlir/nspawn_in_subvol/plugins:plugins",
        "//antlir/nspawn_in_subvol/plugins:rpm",
    ],
)

# This minimal XAR shipped as tests/hello.xar was generated via:
#
#   pip install xar && mkdir -p hello &&
#   echo "print('hello world')" > hello/__main__.py &&
#   python -m xar.make_xar -- --python hello --output hello.xar
#
# NB: Internally, you can use `buck run //tools/xar:make_xar` instead.
image.layer(
    name = "host-hello-xar",
    parent_layer = TEST_IMAGE_PREFIX + "test-layer",
    features = [image.install("tests/hello.xar", "/hello.xar")],
)

python_library(
    name = "testlib_base",
    srcs = ["tests/base.py"],
    deps = [
        ":run-library",
        "//antlir:testlib_layer_resource",
    ],
)

python_unittest(
    name = "test-run",
    srcs = [
        "tests/test_run.py",
    ],
    env = {
        # Needed to cover the `TERM` branch in `booted.py` because FB's test
        # runner otherwise strips out `TERM`.
        "TERM": "linux-clown",
    },
    # This is the integration test for the CLI and underlying library, so
    # it's supposed to cover everything.
    needed_coverage = [(100, target) for target in [
        ":args",
        ":booted",
        ":cmd",
        ":common",
        ":non_booted",
        ":run-library",
    ]],
    resources = {
        layer_resource(
            TEST_IMAGE_PREFIX + "test-layer",
        ): "tests/test-layer",
        layer_resource(
            TEST_IMAGE_PREFIX + "test-layer-with-mounts",
        ): "tests/test-layer-with-mounts",
        layer_resource(
            TEST_IMAGE_PREFIX + "hello_world_base",
        ): "tests/test-hello-world-base",
        layer_resource("//tupperware/image/base:base"): "tests/bootable-systemd-os",
        layer_resource(":host-hello-xar"): "tests/host-hello-xar",
    },
    deps = [":testlib_base"],
)

python_unittest(
    name = "test-slow-sudo",
    srcs = ["tests/test_slow_sudo.py"],
    resources = {
        layer_resource(
            TEST_IMAGE_PREFIX + "build_appliance_testing",
        ): "tests/build-appliance",
    },
    deps = [":testlib_base"],
)

python_binary(
    name = "run-test",
    srcs = ["run_test.py"],
    main_module = "antlir.nspawn_in_subvol.run_test",
    # Ensures we can read resources in @mode/opt. "xar" cannot work because
    # `root` cannot access the content of unprivileged XARs.
    par_style = "zip",
    deps = [
        ":args",
        ":cmd",
        ":run-library",
    ],
)

python_unittest(
    name = "test-run-test",
    srcs = ["tests/test_run_test.py"],
    needed_coverage = [(100, ":run-test-library")],
    # Ensures we can read resources in @mode/opt. "xar" cannot work because
    # `root` cannot access the content of unprivileged XARs.
    par_style = "zip",
    deps = [":run-test-library"],
)
