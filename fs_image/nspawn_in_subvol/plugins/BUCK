load("//fs_image/bzl:image.bzl", "image")
load("//fs_image/bzl:layer_resource.bzl", "layer_resource")
load("//fs_image/bzl:oss_shim.bzl", "python_library", "python_unittest")
load("//fs_image/compiler:defs.bzl", "TEST_IMAGE_PREFIX")

# No test coverage because it has no logic.
python_library(
    name = "plugins",
    srcs = ["__init__.py"],
    deps = ["//fs_image:fs_utils"],
)

python_library(
    name = "testlib_rpm_base",
    srcs = ["tests/rpm_base.py"],
    resources = {
        layer_resource(TEST_IMAGE_PREFIX + "build_appliance_testing"): "tests/build-appliance",
    },
    deps = [
        "//fs_image/nspawn_in_subvol:testlib_base",
        "//fs_image/rpm:find_snapshot",
    ],
)

# Gets test coverage via `:test-repo-servers`
python_library(
    name = "launch_repo_servers",
    srcs = ["launch_repo_servers.py"],
    deps = [
        "//fs_image:common",
        "//fs_image:fs_utils",
    ],
)

python_library(
    name = "repo_servers",
    srcs = ["repo_servers.py"],
    # Future: bring this back, a comment in the `.py` file explains how.
    # resources = {"//fs_image/rpm:repo-server": "repo-server"},
    deps = [
        ":launch_repo_servers",
        "//fs_image:common",
        "//fs_image:fs_utils",
        "//fs_image/nspawn_in_subvol:args",
        "//fs_image/nspawn_in_subvol:plugin_hooks",
    ],
)

python_unittest(
    name = "test-repo-servers",
    srcs = ["tests/test_repo_servers.py"],
    needed_coverage = [
        (100, ":launch_repo_servers"),
        (100, ":repo_servers"),
    ],
    deps = [":testlib_rpm_base"],
)

python_library(
    name = "yum_dnf_versionlock",
    srcs = ["yum_dnf_versionlock.py"],
    deps = [
        "//fs_image:common",
        "//fs_image:fs_utils",
        "//fs_image/nspawn_in_subvol:args",
        "//fs_image/nspawn_in_subvol:plugin_hooks",
    ],
)

python_unittest(
    name = "test-yum-dnf-versionlock",
    srcs = ["tests/test_yum_dnf_versionlock.py"],
    needed_coverage = [
        (100, ":yum_dnf_versionlock"),
        (100, "//fs_image/nspawn_in_subvol:plugin_hooks"),
    ],
    deps = [":testlib_rpm_base"],
)

python_library(
    name = "rpm",
    srcs = ["rpm.py"],
    deps = [
        ":plugins",
        ":repo_servers",
        ":shadow_paths",
        ":yum_dnf_versionlock",
        "//fs_image:common",
        "//fs_image:fs_utils",
    ],
)

python_unittest(
    name = "test-rpm",
    srcs = ["tests/test_rpm.py"],
    needed_coverage = [(100, ":rpm")],
)

python_library(
    name = "shadow_paths",
    srcs = ["shadow_paths.py"],
    deps = [
        "//fs_image:common",
        "//fs_image:fs_utils",
        "//fs_image:subvol_utils",
        "//fs_image/nspawn_in_subvol:args",
        "//fs_image/nspawn_in_subvol:common",
        "//fs_image/nspawn_in_subvol:plugin_hooks",
    ],
)

image.layer(
    name = "shadow-paths-test-layer",
    parent_layer = TEST_IMAGE_PREFIX + "only-for-tests-read-only-host-clone",
    features = [
        # We'll set up shadowing both via links and directly, and both work.
        image.mkdir("/", "real"),
        image.install(
            "tests/shadow_me",
            "real/shadow_me",
            # The user "nobody" will update this via the shadow root.
            user = "nobody",
            mode = "u+rw,og+r",
        ),
        image.install("tests/i_will_shadow", "real_i_will_shadow"),
        image.install("tests/i_will_shadow", "other_file"),
        image.mkdir("/", "link"),
        image.symlink_file("real/shadow_me", "link/shadow_me"),
        image.symlink_file("real_i_will_shadow", "link/i_will_shadow"),
        image.mkdir(
            "/",
            "out",
            user = "nobody",
        ),  # Tests write here
        image.mkdir("/", "other_dir"),
    ],
)

python_unittest(
    name = "test-shadow-paths",
    srcs = ["tests/test_shadow_paths.py"],
    needed_coverage = [(100, ":shadow_paths")],
    resources = {
        layer_resource(":shadow-paths-test-layer"): "tests/shadows",
    },
    deps = [
        "//fs_image:testlib_temp_subvolumes",
        "//fs_image/nspawn_in_subvol:testlib_base",
    ],
)
