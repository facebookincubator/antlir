# @noautodeps

load("@bazel_skylib//lib:paths.bzl", "paths")
load("//antlir/bzl:image.bzl", "image")
load("//antlir/bzl:oss_shim.bzl", "buck_filegroup", "third_party", "thrift_library")
load("//antlir/bzl/image/feature:defs.bzl", "feature")
load("//metalos:defs.bzl", "rust_binary", "rust_library")

# @oss-disable: oss = False 

oss = True # @oss-enable

# Introduce a buckconfig option that will be used only at query time so that we
# can control the target graph before running shipit at all.
# If this becomes used by more than this one TARGETS file (and it really should
# not), it should be extracted to REPO_CFG.
oss_ci = read_config("antlir", "oss-ci-query", "") != ""

thrift_library(
    name = "host",
    languages = ["rust"] + [
        "cpp2",
        "py3",
    ] if not (oss or oss_ci) else [],
    # @oss-disable: py3_namespace = "metalos.host_configs", 
    rust_crate_name = "host",
    rust_deps = ["//metalos/metalos_macros:metalos_macros"] + third_party.libraries(
        [
            "derive_builder",
            "paste",
            "starlark",
            "starlark_derive",
        ],
        platform = "rust",
    ),
    rust_features = [
        # @oss-disable: "facebook", 
    ],
    rust_include_srcs = ["host_thrift.rs"],
    rust_unittests = True,
    thrift_rust_options = "serde",
    thrift_srcs = {"host.thrift": None},
    visibility = [
        "PUBLIC",
    ],
    deps = [
        ":runtime-config",
    ] + [
        "//metalos/host_configs/facebook:host-facebook",
    ] if not (oss or oss_ci) else [],
)

rust_library(
    name = "evalctx",
    srcs = glob(["evalctx/**/*.rs"]),
    env = {
        "TEST_FILES_DIR": "$(location :all_starklark_files)",
    },
    features =
        ["facebook"] if (not oss and not oss_ci) else [],
    test_deps = [
        "maplit",
        "slog_glog_fmt",
        "tempfile",
    ],
    test_srcs = ["generators/hostname.star"],
    deps = [
        "anyhow",
        "derive_more",
        "handlebars",
        "once_cell",
        "serde",
        "serde_json",
        "slog",
        "slog_glog_fmt",
        "starlark",
        "starlark_derive",
        "thiserror",
        "walkdir",
        "xattr",
        ":all_starklark_files",
        ":host-rust",
        "//metalos/lib:shadow",
    ],
)

buck_filegroup(
    name = "all_starklark_files",
    srcs = glob(["**/*.star"]),
)

# This binary is useful to manually check what a host config generator would
# produce for a given host input, without having to boot a physical server or go
# through vmtest
# Usage:
#   construct a host json input (by hand or download it from the proxy service in facebook/service)
#   buck run //metalos/host_configs:evaluator -- host.json path/to/generator.star
rust_binary(
    name = "evaluator",
    srcs = glob(["evaluator/**/*.rs"]),
    visibility = [],
    deps = [
        "anyhow",
        "serde_json",
        "slog",
        "slog_glog_fmt",
        "structopt",
        ":evalctx",
    ],
)

feature.new(
    name = "generators",
    features = [
        image.ensure_subdirs_exist("/usr/lib/metalos", "generators"),
    ] + [
        feature.install(
            src,
            "/usr/lib/metalos/generators/{}".format(paths.basename(src)),
        )
        for src in glob(["generators/*.star"])
    ],
    visibility = ["//metalos/..."],
)

thrift_library(
    name = "package-manifest",
    languages = ["rust"] + [
        "cpp2",
        "py3",
    ] if not (oss or oss_ci) else [],
    # @oss-disable: py3_namespace = "metalos.host_configs", 
    thrift_rust_options = "serde",
    thrift_srcs = {"package_manifest.thrift": None},
    visibility = [
        "PUBLIC",
    ],
)

thrift_library(
    name = "runtime-config",
    languages = ["rust"] + [
        "cpp2",
        "py3",
    ] if not (oss or oss_ci) else [],
    # @oss-disable: py3_namespace = "metalos.host_configs", 
    thrift_rust_options = "serde",
    thrift_srcs = {"runtime_config.thrift": None},
    visibility = [
        "PUBLIC",
    ],
    deps = [
        ":package-manifest",
    ],
)
