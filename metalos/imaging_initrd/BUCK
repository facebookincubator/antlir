load("//antlir/bzl:constants.bzl", "REPO_CFG")
load("//antlir/bzl:flavor_helpers.bzl", "flavor_helpers")
load("//antlir/bzl:image.bzl", "image")
load("//antlir/bzl:systemd.bzl", "systemd")
load("//antlir/bzl/genrule/extractor:extract.bzl", "extract")
load("//antlir/bzl/image/feature:defs.bzl", "feature")
load("//antlir/bzl/image/package:defs.bzl", "package")

imaging_initrd_wanted_rpms = [
    "nvme-cli",  # For wiping disks / restoring ssd performance
    "efibootmgr",  # Instaling the bootloader
]

image.layer(
    name = "with-imagining-initrd-bins",
    parent_layer = REPO_CFG.artifact["metalos.layer.base"],
    features = [
        feature.rpms_install(imaging_initrd_wanted_rpms),
        feature.install(
            "//metalos/imaging_initrd/metalimage:metalimage",
            "/usr/bin/metalimage",
            mode = "a+rx",
        ),
        feature.install(
            "//metalos/imaging_initrd/network_generator:network_generator",
            "/usr/lib/systemd/system-generators/metalos-network-generator",
            mode = "a+rx",
        ),
    ],
)

image.layer(
    name = "imaging-initrd",
    parent_layer = "//metalos/initrd:initrd-common",
    features = [
        systemd.install_unit("units/metalos-reimage.service"),
        systemd.enable_unit("metalos-reimage.service", "initrd.target", "requires"),
        # these will never work on their own and will produce very confusing
        # output on the console - we are special snowflakes and have to have
        # metalimage setup and actually execute kexec
        feature.ensure_subdirs_exist("/etc", "systemd/system"),
        systemd.mask_units(
            [
                "initrd-cleanup.service",
                "initrd-parse-etc.service",
                "initrd-switch-root.service",
                "initrd-switch-root.target",
            ],
        ),
        extract.extract(
            binaries = [
                "/usr/lib/systemd/system-generators/metalos-network-generator",
                "/usr/bin/metalimage",
                "/usr/sbin/nvme",
                "/usr/sbin/efibootmgr",
            ],
            dest = "/",
            source = ":with-imagining-initrd-bins",
        ),
    ],
    flavor = flavor_helpers.get_antlir_linux_flavor(),
    visibility = [
        "//antlir/vm/...",
        "//metalos/...",
    ],
)

package.new(
    name = "imaging-initrd.cpio.gz",
    format = "cpio.gz",
    layer = ":imaging-initrd",
    visibility = ["PUBLIC"],
)
