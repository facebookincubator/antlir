# @noautodeps

load("@bazel_skylib//lib:paths.bzl", "paths")
load("//antlir/bzl:constants.bzl", "REPO_CFG")
load("//antlir/bzl:image.bzl", "image")
load("//antlir/bzl:kernel_shim.bzl", "kernels")
load("//antlir/bzl:oss_shim.bzl", "buck_genrule", "third_party")
load("//antlir/bzl:systemd.bzl", "systemd")
load("//antlir/bzl/genrule/extractor:extract.bzl", "extract")
load("//antlir/bzl/image/feature:defs.bzl", "feature")
load("//antlir/bzl/image/package:btrfs.bzl", "btrfs")
load("//antlir/bzl/image/package:defs.bzl", "package")
load("//antlir/linux/vm/console:defs.bzl", "console")
load("//antlir/linux/vm/generator:defs.bzl", "generator")
load("//antlir/linux/vm/network:defs.bzl", "network")
load("//antlir/linux/vm/notify:defs.bzl", "notify")
load("//antlir/linux/vm/ssh:defs.bzl", "ssh")
load("//antlir/vm/bzl:defs.bzl", "vm")
load("//metalos:defs.bzl", "rust_binary")
load("//metalos/disk:disk.bzl", "disk")
load("//metalos/host_configs/tests:host.bzl", "host_config")

TEST_KERNEL = kernels.get("5.6.13-0_fbk7_4335_g9d5dc3fbc3c2")

image.layer(
    name = "deps",
    parent_layer = "//metalos/os:metalos",
    features = [
        image.rpms_install([
            "dropbear",
        ]),
    ],
    visibility = ["//metalos/initrd/..."],
)

binaries = extract.extract(
    binaries = [
        "/usr/sbin/dropbear",
        "/usr/bin/dropbearkey",
        # this is unlikely to be useful in the base initrd, so keep it here in
        # vmtest-land unless anything changes
        "/usr/bin/systemd-run",
    ],
    dest = "/",
    source = ":deps",
)

ssh_server = [
    image.clone(":deps", "/usr/lib/systemd/system/dropbear.service", "/usr/lib/systemd/system/dropbear.service"),
    image.ensure_subdirs_exist("/usr/lib", "tmpfiles.d"),
    feature.install("dropbear.tmpfiles.conf", "/usr/lib/tmpfiles.d/dropbear.conf"),
    image.ensure_dirs_exist("/etc/sysconfig"),
    feature.install("dropbear", "/etc/sysconfig/dropbear"),
    systemd.install_unit("dropbear-keygen.service"),
    systemd.enable_unit(
        "dropbear.service",
        target = "initrd.target",
    ),
    # vmtest synchronizes on sshd.service, so alias it to dropbear
    feature.ensure_file_symlink("/usr/lib/systemd/system/dropbear.service", "/usr/lib/systemd/system/sshd.service"),
    image.ensure_dirs_exist("/root"),
    ssh.test_only_login(),
]

par_support = feature.install(
    "bash",
    "/usr/bin/bash",
    mode = "a+rx",
)

host_config(
    name = "host-config",
    host_config = {
        "boot_config": {
            "deployment_specific": {"metalos": {}},
            "initrd": {
                "format": 2,
                "id": {"uuid": "deadbeefdeadbeefdeadbeefdeadbeef"},
                "kind": 3,
                "name": "metalos.initrd",
                "override_uri": None,
            },
            "kernel": {
                "pkg": {
                    "format": 1,
                    "id": {"uuid": "deadbeefdeadbeefdeadbeefdeadbeef"},
                    "kind": 2,
                    "name": "kernel." + TEST_KERNEL.uname,
                    "override_uri": "http://vmtest-host:8000/package/kernel.{}:deadbeefdeadbeefdeadbeefdeadbeef".format(TEST_KERNEL.uname),
                },
            },
            "rootfs": {
                "format": 1,
                "id": {"uuid": "deadbeefdeadbeefdeadbeefdeadbeef"},
                "kind": 1,
                "name": "metalos",
                "override_uri": "http://vmtest-host:8000/package/metalos:deadbeefdeadbeefdeadbeefdeadbeef",
            },
        },
        "provisioning_config": {
            "deployment_specific": {"metalos": {}},
            "gpt_root_disk": {
                "format": 2,
                "id": {"uuid": "deadbeefdeadbeefdeadbeefdeadbeef"},
                "kind": 7,
                "name": "metalos.gpt-root-disk",
                "override_uri": "http://vmtest-host:8000/package/metalos.gpt-root-disk:deadbeefdeadbeefdeadbeefdeadbeef",
            },
            "imaging_initrd": {
                "format": 2,
                "id": {"uuid": "deadbeefdeadbeefdeadbeefdeadbeef"},
                "kind": 4,
                "name": "metalos.imaging-initrd",
                "override_uri": None,
            },
        },
        "runtime_config": {
            "deployment_specific": {"metalos": {}},
        },
    },
)

test_kernel_modules = [
    image.ensure_subdirs_exist("/usr/lib", "modules"),
    feature.install(
        TEST_KERNEL.derived_targets.disk_boot_modules,
        paths.join(
            "/usr/lib/modules",
            TEST_KERNEL.uname,
        ),
    ),
]

# Generic features that are needed to support vmtests that _stay_ in the
# initrd, meant to test the initrd itself.
image.layer(
    name = "initrd-vmtest",
    parent_layer = "//metalos/initrd:initrd",
    features = [
        # shared features straight from vmtest
        console.autologin(),
        generator.mounts(),
        feature.remove(
            "/etc/resolv.conf",
            must_exist = False,
        ),
        network.host(),
        par_support,
        ssh_server,
        notify.install(),
        # initrd-specific things below:
        binaries,
        # Some fixups for vmtest features in the limited initrd environment
        feature.install("udev.rules", "/usr/lib/udev/rules.d/50-vmtest.rules"),
        systemd.install_dropin("after-network.conf", "sshd.service"),
        # don't use the full test-kernel initrd, but we do need the modules
        test_kernel_modules,
        # We want to stay in the initrd, so mask out the units that switch-root
        # into the base os
        image.ensure_dirs_exist("/etc/systemd/system"),
        systemd.mask_units([
            "initrd-cleanup.service",
            "initrd-parse-etc.service",
            "initrd-switch-root.service",
        ]),
        # install a host config in the initrd directly
        image.ensure_subdirs_exist("/", "test"),
        feature.install(":host-config", "/test/fake-host-config.json"),
    ],
    flavor = REPO_CFG.antlir_linux_flavor,
)

package.new(
    name = "vmtest-initrd.cpio.gz",
    format = "cpio.gz",
    layer = ":initrd-vmtest",
    visibility = [],
)

vm.rust_unittest(
    name = "boots",
    vm_opts = vm.types.opts.new(
        initrd = ":vmtest-initrd.cpio.gz",
        kernel = TEST_KERNEL,
    ),
    srcs = ["test_boots.rs"],
    crate_root = "test_boots.rs",
)

vm.rust_unittest(
    name = "slaac",
    vm_opts = vm.types.opts.new(
        initrd = ":vmtest-initrd.cpio.gz",
        kernel = TEST_KERNEL,
        append = [
            "macaddress=11:22:33:44:55:66",
        ],
    ),
    srcs = ["test_slaac.rs"],
    crate_root = "test_slaac.rs",
    deps = [
        third_party.library(
            name,
            platform = "rust",
        )
        for name in [
            "nix",
        ]
    ],
)

# This needs to be as close as possible the prod initrd, with only the bare
# minimum of extra features to get baseline functionality in vmtest. Tests that
# exercise this initrd are run in the post-switch-root image, so this can be as
# close to the base as possible.
image.layer(
    name = "switch-root-initrd",
    parent_layer = "//metalos/initrd:initrd",
    features = [
        feature.remove("/etc/resolv.conf"),
        network.host(),
        # don't use the full test-kernel initrd, but we do need the modules
        test_kernel_modules,
        # install a host config in the initrd directly
        image.ensure_subdirs_exist("/", "test"),
        feature.install(":host-config", "/test/fake-host-config.json"),
    ],
    flavor = REPO_CFG.antlir_linux_flavor,
)

package.new(
    name = "switch-root-initrd.cpio.gz",
    layer = ":switch-root-initrd",
    format = "cpio.gz",
    visibility = [],
)

image.layer(
    name = "metalos-vmtest",
    parent_layer = "//metalos/os:metalos",
    features = [
        ssh.test_only_login(),
        console.autologin(),
        generator.mounts(),
        feature.remove("/etc/hosts"),
        network.host(),
        notify.install(),
        # Technically, sshd.service is already enabled in the base metalos
        # image, but this indicates that in the vmtest use-case, sshd _is_ the
        # workload.
        systemd.enable_unit("sshd.service", "workload.target"),
    ],
    flavor = REPO_CFG.antlir_linux_flavor,
)

package.new(
    name = "metalos-vmtest.sendstream.zst",
    layer = ":metalos-vmtest",
    format = "sendstream.zst",
)

image.layer(
    name = "empty-layer",
    flavor = REPO_CFG.antlir_linux_flavor,
)

# We need to build a new gpt image for the switch-root
# test so that the image is sized correctly. Otherwise we
# keep the image minimal and expand at runtime which we don't
# do for existing boots
btrfs.new(
    name = "rootdisk.btrfs",
    opts = btrfs.opts.new(
        subvols = {
            "/volume": btrfs.opts.subvol.new(
                layer = "//metalos/disk:control",
                writable = True,
            ),
        },
        default_subvol = "/volume",
        loopback_opts = image.opts(
            label = "/",
            size_mb = 4096,
        ),
    ),
)

disk(
    name = "metalos-gpt-image",
    efi_vfat = "//metalos/disk:empty-efi.vfat",
    root_btrfs = ":rootdisk.btrfs",
)

vm.rust_unittest(
    name = "switch-root",
    vm_opts = vm.types.opts.new(
        initrd = ":switch-root-initrd.cpio.gz",
        kernel = TEST_KERNEL,
        append = [
            "metalos.host-config-uri=file:///test/fake-host-config.json",
            "rd.systemd.journald.forward_to_console=1",
        ],
        runtime = vm.types.runtime.new(
            sidecar_services = ["$(exe :images-sidecar) $(location :image_packages)"],
        ),
        disk = vm.types.disk.new(
            package = ":metalos-gpt-image",
        ),
    ),
    timeout_secs = 600,
    srcs = ["test_switch_root.rs"],
    deps = ["//metalos/lib/systemd:systemd"] + third_party.libraries(
        [
            "anyhow",
            "nix",
            "slog",
            "slog_glog_fmt",
            "tokio",
        ],
        platform = "rust",
    ),
    crate_root = "test_switch_root.rs",
    # TODO(vmagro): re-enable this when the container runtime environment is
    # done and the sidecars can reliably run on Sandcastle
    tags = ["local_only"],
)

vm.rust_unittest(
    name = "switch-root-reimage",
    vm_opts = vm.types.opts.new(
        initrd = ":switch-root-initrd.cpio.gz",
        kernel = TEST_KERNEL,
        append = [
            "metalos.host-config-uri=file:///test/fake-host-config.json",
            "metalos.write-root-disk-package=metalos_root:1",
            "rd.systemd.journald.forward_to_console=1",
        ],
        runtime = vm.types.runtime.new(
            sidecar_services = ["$(exe :images-sidecar) $(location :image_packages)"],
        ),
        disk = vm.types.disk.new(
            layer = ":empty-layer",
            layer_size_mb = 1024 * 3,
            # because this isn't actually the partition we want to mount we give this a
            # label that isn't / and then still mount / which should find the newly
            # imaged partition
            layer_label = "empty_root_disk",
        ),
        root_label = "/",
    ),
    # unfortunately this test is REAL slow because it takes so long to download
    # the two images and write them to disk
    timeout_secs = 600,
    srcs = ["test_switch_root.rs"],
    deps = ["//metalos/lib/systemd:systemd"] + third_party.libraries(
        [
            "anyhow",
            "nix",
            "slog",
            "slog_glog_fmt",
            "tokio",
        ],
        platform = "rust",
    ),
    crate_root = "test_switch_root.rs",
    # TODO(vmagro): re-enable this when the container runtime environment is
    # done and the sidecars can reliably run on Sandcastle
    tags = ["local_only"],
)

rust_binary(
    name = "images-sidecar",
    srcs = ["images_sidecar.rs"],
    crate_root = "images_sidecar.rs",
    deps = [
        "anyhow",
        "tokio",
        "warp",
    ],
)

package.new(
    name = "kernel.{}.sendstream.zst".format(TEST_KERNEL.uname),
    layer = TEST_KERNEL.derived_targets.image,
    format = "sendstream.zst",
)

buck_genrule(
    name = "image_packages",
    out = ".",
    cmd = """
        cp --reflink=auto $(location :metalos-vmtest.sendstream.zst) $OUT/metalos:deadbeefdeadbeefdeadbeefdeadbeef
        zstd -i $(location //metalos/disk:metalos-gpt-image) -o $OUT/metalos.gpt-root-disk:deadbeefdeadbeefdeadbeefdeadbeef
        cp --reflink=auto $(location :kernel.{}.sendstream.zst) $OUT/kernel.{}:deadbeefdeadbeefdeadbeefdeadbeef
    """.format(
        TEST_KERNEL.uname,
        TEST_KERNEL.uname,
    ),
)

vm.rust_unittest(
    name = "images",
    vm_opts = vm.types.opts.new(
        initrd = ":vmtest-initrd.cpio.gz",
        kernel = TEST_KERNEL,
        append = [
            "metalos.host-config-uri=file:///test/fake-host-config.json",
            "rd.systemd.journald.forward_to_console=1",
        ],
        runtime = vm.types.runtime.new(
            sidecar_services = [
                "$(exe :images-sidecar) $(location :image_packages)",
            ],
        ),
        disk = vm.types.disk.new(
            layer = "//metalos/disk:control",
            layer_size_mb = 4096,
        ),
    ),
    deps = [
        third_party.library(
            name,
            platform = "rust",
        )
        for name in [
            "anyhow",
        ]
    ],
    srcs = ["test_images.rs"],
    crate_root = "test_images.rs",
)
