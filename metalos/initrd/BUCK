# @noautodeps

load("//antlir/bzl:constants.bzl", "REPO_CFG")
load("//antlir/bzl:flavor_helpers.bzl", "flavor_helpers")
load("//antlir/bzl:image.bzl", "image")
load("//antlir/bzl:systemd.bzl", "systemd")
load("//antlir/bzl/genrule/extractor:extract.bzl", "extract")
load("//antlir/bzl/image/feature:defs.bzl", "feature")
load("//antlir/bzl/image/feature:usergroup.bzl", "SHELL_NOLOGIN")
load("//antlir/bzl/image/package:defs.bzl", "package")
load("//antlir/bzl/linux:busybox.bzl", "DEFAULT_APPLETS")
load("//antlir/bzl/linux:defs.bzl", "linux")
load(":systemd.bzl", "clone_systemd_configs", SYSTEMD_BINARIES = "BINARIES")

dbus = [
    feature.group_add("dbus"),
    feature.user_add("dbus", "dbus", "/", SHELL_NOLOGIN),
]

networking = [
    # Explicitly enable systemd-networkd to configure the network links
    # early in boot
    systemd.enable_unit("systemd-networkd.service"),
    systemd.enable_unit("systemd-networkd-wait-online.service"),
    # There is some opportunity to consolidate with the MetalOS root network
    # unit, but for right now we only want to use SLAAC in the initrd.
    feature.install("units/eth.network", "/usr/lib/systemd/network/50-eth.network"),
]

nss = linux.config.glibc.nsswitch.install(linux.config.glibc.nsswitch.default)

users = [
    feature.install("group", "/etc/group"),
    feature.install("passwd", "/etc/passwd"),
    feature.install("//antlir:empty", "/etc/shadow"),
    feature.install("//antlir:empty", "/usr/sbin/nologin"),
    feature.group_add("systemd-network"),
    feature.user_add(
        "systemd-network",
        "systemd-network",
        "/",
        SHELL_NOLOGIN,
    ),
]

udev = [
    image.ensure_subdirs_exist("/usr/lib/", "udev/rules.d"),
] + [
    feature.install(
        "udev/{}".format(f),
        "/usr/lib/udev/rules.d/{}".format(f),
    )
    for f in [
        "10-dm.rules",
        "50-udev-default.rules",
        "60-block.rules",
        "60-persistent-storage.rules",
        "80-drivers.rules",
        "95-dm-notify.rules",
        "99-systemd.rules",
    ]
]

debug = [
    systemd.install_dropin("debug-shell.conf", "debug-shell.service"),
    systemd.install_dropin("emergency.conf", "emergency.service"),
]

image.layer(
    name = "busybox",
    flavor = flavor_helpers.get_antlir_linux_flavor(),
    features = [
        image.rpms_install([
            "busybox",
        ]),
    ],
    visibility = ["//metalos/initrd/..."],
)

# A bare layer with only systemd essentials included
image.layer(
    name = "bare-systemd",
    features = [
        linux.filesystem.install(),
        clone_systemd_configs(REPO_CFG.artifact["metalos.layer.base"]),
        feature.ensure_file_symlink("/usr/lib/systemd/systemd", "/init"),
        # Systemd uses the presence of /etc/initrd-release to determine if it
        # is running in an initrd, however there are cases where it only parses
        # /{etc,usr/lib}/os-release, so just cover all our bases with symlinks
        linux.release.install(
            path = "/usr/lib/initrd-release",
            layer = ":bare-systemd",
            os_name = "MetalOS",
            variant = "Initrd",
        ),
        feature.ensure_file_symlink("/usr/lib/initrd-release", "/etc/initrd-release"),
        feature.ensure_file_symlink("/usr/lib/initrd-release", "/etc/os-release"),
        feature.ensure_file_symlink("/usr/lib/initrd-release", "/usr/lib/os-release"),
        # With our custom setup, just enable initrd-cleanup.service directly in
        # initrd.target instead of going through initrd-parse-etc.service as normal
        systemd.enable_unit(
            "initrd-cleanup.service",
            target = "initrd.target",
        ),
        systemd.install_dropin(
            "dropins/reload-before-cleanup.conf",
            unit = "initrd-cleanup.service",
        ),
        debug,
        dbus,
        nss,
        users,
        udev,
        extract.extract(
            binaries = SYSTEMD_BINARIES + [
                # Metalctl uses libblkid for device discover, so include
                # blkid.8 for debugging purposes.
                "/usr/sbin/blkid",
                "/usr/sbin/btrfs",
            ],
            dest = "/",
            source = REPO_CFG.artifact["metalos.layer.base"],
        ),
    ],
    flavor = flavor_helpers.get_antlir_linux_flavor(),
    visibility = [
        "//metalos/bootloader/...",
        "//metalos/imaging_initrd/...",
        "//ti/platform/edgeos/...",
    ],
)

# things we expect to exist in all the metalos related initrds
image.layer(
    name = "initrd-common",
    parent_layer = ":bare-systemd",
    features = [
        linux.busybox.install(
            src = ":busybox",
            src_path = "/usr/sbin/busybox",
            applets = DEFAULT_APPLETS,
        ),
        networking,
        # The metalos generator will rewrite the default target to
        # initrd.target, so explicitly set emergency.target as the default in
        # the image so that the boot clearly fails if the generator fails during
        # early boot for any reason
        systemd.set_default_target("emergency.target"),
        image.ensure_subdirs_exist("/usr/lib", "tmpfiles.d"),
        feature.install("units/metalos.tmpfiles.conf", "/usr/lib/tmpfiles.d/metalos.conf"),
        # @oss-disable: "//metalos/initrd/facebook:features", 
    ],
    flavor = flavor_helpers.get_antlir_linux_flavor(),
    visibility = [
        "//antlir/vm/...",
        "//metalos/...",
    ],
)

image.layer(
    name = "with-initrd-bins",
    parent_layer = REPO_CFG.artifact["metalos.layer.base"],
    features = [
        feature.install(
            "//metalos/initrd/metalinit:metalinit",
            "/usr/bin/metalinit",
            mode = "a+rx",
        ),
        feature.install(
            "//metalos/initrd/network_generator:network_generator",
            "/usr/lib/systemd/system-generators/metalos-network-generator",
            mode = "a+rx",
        ),
    ],
)

# Kernel-agnostic base initrd
image.layer(
    name = "initrd",
    parent_layer = ":initrd-common",
    features = [
        systemd.install_unit("units/metalos-init.service"),
        systemd.enable_unit("metalos-init.service", "initrd-switch-root.target", "requires"),
        extract.extract(
            binaries = [
                "/usr/lib/systemd/system-generators/metalos-network-generator",
                "/usr/bin/metalinit",
            ],
            dest = "/",
            source = ":with-initrd-bins",
        ),
    ],
    flavor = flavor_helpers.get_antlir_linux_flavor(),
    visibility = [
        "//metalos/...",
        "//scripts/dschatzberg/vm:",
    ],
)

package.new(
    name = "initrd.cpio.gz",
    format = "cpio.gz",
    layer = ":initrd",
    visibility = ["PUBLIC"],
)
