load("//antlir/bzl:image.bzl", "image")
load("//antlir/bzl:oss_shim.bzl", "kernel_get")
load("//antlir/bzl:systemd.bzl", "systemd")
load("//antlir/bzl/image/feature:defs.bzl", "feature")
load("//antlir/bzl/image/package:defs.bzl", "package")
load("//antlir/linux/vm/console:defs.bzl", "console")
load("//antlir/linux/vm/generator:defs.bzl", "generator")
load("//antlir/linux/vm/network:defs.bzl", "network")
load("//antlir/linux/vm/notify:defs.bzl", "notify")
load("//antlir/linux/vm/ssh:defs.bzl", "ssh")

snapshot_fedora_override = image.opts(
    rpm_repo_snapshot = "//snapshot:fedora33",
)

image.layer(
    name = "fedora",
    features = [
        image.rpms_install([
            "basesystem",
            "coreutils",
        ]),
    ],
    flavor_config_override = snapshot_fedora_override,
    visibility = ["//images/..."],
)

vm_features = [
    image.rpms_install([
        "btrfs-progs",
        "iproute",
        "openssh-server",
        "sudo",
    ]),
    console.autologin(),
    network.host(),
    generator.mounts(),
    notify.install(),
    ssh.test_only_login(),
    # Set a default hostname
    # NOTE: this will ultimately become dynamic, this is temporary
    # to unblock work.
    image.install("hostname", "/etc/hostname"),
    # We want to allow logins before the system is finished booting
    # up.
    feature.remove("/usr/lib/tmpfiles.d/systemd-nologin.conf"),
    # systemd-networkd isn't enabled by default in fedora.
    systemd.enable_unit("systemd-networkd.service"),
]

image.layer(
    name = "fedora.vm",
    parent_layer = ":fedora",
    features = vm_features,
    flavor = "fedora33",
    flavor_config_override = snapshot_fedora_override,
    runtime = ["systemd"],
)

package.new(
    name = "fedora.vm.btrfs",
    format = "btrfs",
    layer = ":fedora.vm",
    loopback_opts = image.opts(
        seed_device = True,
        writable_subvolume = True,
    ),
    visibility = [
        "//antlir/vm/...",
        kernel_get.base_target + "/...",
    ],
)

snapshot_centos_override = image.opts(
    # This is the default, but I am making it explicit since the target is
    # named centos
    rpm_repo_snapshot = "//snapshot:centos8-stream",
)

image.layer(
    name = "centos",
    features = [
        image.rpms_install([
            "basesystem",
            "coreutils",
        ]),
    ],
    flavor_config_override = snapshot_centos_override,
    visibility = ["//images/..."],
)

image.layer(
    name = "centos.vm",
    parent_layer = ":centos",
    features = vm_features,
    flavor = "centos8",
    flavor_config_override = snapshot_fedora_override,
    runtime = ["systemd"],
)

package.new(
    name = "centos.vm.btrfs",
    format = "btrfs",
    layer = ":centos.vm",
    loopback_opts = image.opts(
        seed_device = True,
        writable_subvolume = True,
    ),
    visibility = [
        "//antlir/vm/...",
        kernel_get.base_target + "/...",
    ],
)
