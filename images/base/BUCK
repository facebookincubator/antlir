load("//antlir/bzl:image.bzl", "image")
load("//antlir/bzl:oss_shim.bzl", "kernel_get")
load("//antlir/bzl:systemd.bzl", "systemd")
load("//antlir/bzl/image/feature:defs.bzl", "feature")
load("//antlir/bzl/image/package:btrfs.bzl", "btrfs")
load("//antlir/linux/vm/console:defs.bzl", "console")
load("//antlir/linux/vm/generator:defs.bzl", "generator")
load("//antlir/linux/vm/network:defs.bzl", "network")
load("//antlir/linux/vm/notify:defs.bzl", "notify")
load("//antlir/linux/vm/ssh:defs.bzl", "ssh")

package(name = "twimage")

snapshot_fedora_override = image.opts(
    # This is the default, but I am making it explicit since the target is
    # named fedora
    rpm_repo_snapshot = "//snapshot:fedora33",
)

image.layer(
    name = "fedora",
    features = [
        feature.rpms_install([
            "basesystem",
            "coreutils",
            "jq",
            "less",
        ]),
    ],
    flavor_config_override = snapshot_fedora_override,
    visibility = ["//images/..."],
)

image.layer(
    name = "fedora.vm",
    features = [
        feature.rpms_install([
            "btrfs-progs",
            "iproute",
            "openssh-server",
            "sudo",
        ]),
        console.autologin(),
        network.host(),
        generator.mounts(),
        notify.install(),
        ssh.test_only_login(),
        # Set a default hostname
        # NOTE: this will ultimately become dynamic, this is temporary
        # to unblock work.
        feature.install("hostname", "/etc/hostname"),
        # We want to allow logins before the system is finished booting
        # up.
        feature.remove("/usr/lib/tmpfiles.d/systemd-nologin.conf"),
        # systemd-networkd isn't enabled by default in fedora.
        systemd.enable_unit("systemd-networkd.service"),
    ],
    flavor = "fedora33",
    flavor_config_override = snapshot_fedora_override,
    parent_layer = ":fedora",
    runtime = ["systemd"],
)

btrfs.new(
    name = "fedora.vm.btrfs",
    opts = btrfs.opts.new(
        loopback_opts = image.opts(
            seed_device = True,
        ),
        subvols = {
            "/volume": btrfs.opts.subvol.new(
                layer = ":fedora.vm",
                writable = True,
            ),
        },
    ),
    visibility = [
        "//antlir/vm/...",
        kernel_get.base_target + "/...",
    ],
)
